# Maintainer: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Renan Birck <renan.ee.ufsm at gmail.com>
 
pkgname=logisim
_truepkgname=logisim-generic
pkgver=2.7.1
pkgrel=5
pkgdesc='An educational tool for designing and simulating digital logic circuits'
arch=('any')
url="http://ozark.hendrix.edu/~burch/logisim/"
license=('GPL2')
depends=('java-runtime' 'gtk-update-icon-cache')
install=logisim.install
source=(http://downloads.sourceforge.net/sourceforge/circuit/${_truepkgname}-${pkgver}.jar
        ${pkgname}.desktop
        ${pkgname}.sh)
sha256sums=('362a78c12ad18c203fed868872c4a01cd9c12141379d92e892bbe2c37e627bc2'
            '4eae5a41f358a77adf69612b03cb3c6f245610d5bcd1b49927c968c6f6d5912e'
            '56eaac407f6ae64289ab228269edb8b1a2bbe647e2baf5dfab7f481c9db8e680')

package() {
  cd "$srcdir"

  install -Dm644 ${_truepkgname}-${pkgver}.jar "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"
  install -Dm644 ${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  for SIZE in 16 20 24 48 64 128; do
    install -Dm644 resources/${pkgname}/img/${pkgname}-icon-${SIZE}.png \
      "${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/${pkgname}.png"
  done
  install -Dm755 ${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"
}
