# Contributor: Roger Braun <davinelulinvega@gmail.com>
# Based on work by: Lee.MaRS <leemars@gmail.com>
# Contributor: kevku <kevku@gmx.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=acroread-fonts-systemwide
pkgver=10
pkgrel=2
arch=('any')
pkgdesc="Fonts from Adobe Acrobat Reader 10: Adobe Arabic, Adobe Hebrew, Adobe Heiti Std, Adobe Ming Std, Adobe Myungjo Std, Adobe Pi Std, Adobe Song Std, Adobe Thai, Courier Std, Minion Pro, Myriad CAD, Myriad Pro, Adobe Fan Heiti Std, Adobe Gothic Std, Kozuka Gothic Pr6N, Kozuka Mincho Pr6N"
url="http://www.adobe.com/products/acrobat/acrrasianfontpack.html"
source=("http://ardownload.adobe.com/pub/adobe/reader/win/10.x/10.0.0/misc/FontPack1000_ja_JP.msi"
        "http://ardownload.adobe.com/pub/adobe/reader/win/10.x/10.0.0/misc/FontPack1000_ko_KR.msi"
        "http://ardownload.adobe.com/pub/adobe/reader/win/10.x/10.0.0/misc/FontPack1000_Xtd_Lang.msi"
        "http://ardownload.adobe.com/pub/adobe/reader/win/10.x/10.0.0/misc/FontPack1000_zh_CN.msi"
        "http://ardownload.adobe.com/pub/adobe/reader/win/10.x/10.0.0/misc/FontPack1000_zh_TW.msi"
        "http://ardownload.adobe.com/pub/adobe/reader/win/10.x/10.1.2/en_US/AdbeRdr1012_en_US.exe" )
makedepends=('p7zip')
noextract=('AdbeRdr1012_en_US.exe')
install=acroread-fonts-systemwide.install
license=('custom')
md5sums=('857220c3513ee42d932dea8c1c1abfca'
         '5dc40f6d8196c119fd9d6e4e236933a0'
         '101ebc3f6c80088f0e8bca83d67c8c58'
         'ea955cbdc33fc22bc2582f7d750e581a'
         '40a50650d1ff02c51d688341244b3ae9'
         'a8fd47ec1de9369f835bd707bd5f4ddb')

package() {
    cd "$srcdir"
    7z -y e AdbeRdr1012_en_US.exe
    7z -o"$pkgdir/usr/share/fonts/adobe" -ir\!*.otf e FontPack1000_ja_JP.msi
    7z -o"$pkgdir/usr/share/fonts/adobe" -ir\!*.otf e FontPack1000_ko_KR.msi
    7z -o"$pkgdir/usr/share/fonts/adobe" -ir\!*.otf e FontPack1000_Xtd_Lang.msi
    7z -o"$pkgdir/usr/share/fonts/adobe" -ir\!*.otf e FontPack1000_zh_CN.msi
    7z -o"$pkgdir/usr/share/fonts/adobe" -ir\!*.otf e FontPack1000_zh_TW.msi
    7z -o"$pkgdir/usr/share/fonts/adobe" -ir\!*.otf e Data1.cab
    7z -o"$pkgdir/usr/share/licenses/$pkgname" -ir\!*.html19 e Data1.cab
    mv "$pkgdir/usr/share/licenses/$pkgname/license.html19" "$pkgdir/usr/share/licenses/$pkgname/license.html"
    chmod -R go+rX "$pkgdir"
}

